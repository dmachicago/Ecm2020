<html lang="en">
<head>
<META content="text/html; charset=utf-8" http-equiv=Content-Type>
<META name="Generator" content="Visustin">
<META name="description" content="T-SQL flow chart saved by Aivosto Visustin (www.aivosto.com).">
<META name="keywords" content="T-SQL, flow graph, diagram">

<title>T-SQL flow chart: proc_RemoveAllCmsUserForeignKeys.sql</title>

<style type="text/css">
<!--
BODY      { margin: 0; }
TEXTAREA  { background: #ffffcc; font-size: 8pt; }
A:link, A:visited { color: blue; }
TR.bar    { background: white; font-family: Verdana, Arial, Sans-Serif; font-size: 70%; }
TR TD     { padding: 0; background: white; }
TR.bar TD { padding: 2px 5px 2px 5px; background: #ccccff; }
-->
</style>

<script type="text/javascript">
<!--
function hidecode(id) { 
  if (id.style.display=="none") {
    id.style.display="";
    hidelink.innerHTML="Hide code";
  } else {
    id.style.display="none";
    hidelink.innerHTML="Show code";
  }
}
-->
</script>
</head>


<body scroll=no>

<table width="100%" height="100%" cellspacing="0" cellpadding="0" border="0">

<tr height="100%"><td id="#upper" colspan="2">
 <iframe name="pic" width="100%" height="100%" src="proc_RemoveAllCmsUserForeignKeys.png" frameborder="0" title="T-SQL flow chart: proc_RemoveAllCmsUserForeignKeys.sql">
  <a href="proc_RemoveAllCmsUserForeignKeys.png"><img src="proc_RemoveAllCmsUserForeignKeys.png" height="100%" title="T-SQL flow chart: proc_RemoveAllCmsUserForeignKeys.sql"></a>
 </iframe>
</td></tr>

<tr id="sourcecode"><td colspan="2">
<textarea rows=15 readonly=yes style="width:100%; border-width:0;">


-- use KenticoCMS_DataMart_2
-- proc_RemoveAllCmsUserForeignKeys.sql

GO
PRINT 'Executing proc_RemoveAllCmsUserForeignKeys.sql';
GO
IF EXISTS (SELECT
          name
     FROM sys.procedures
     WHERE
          name = 'proc_RemoveAllCmsUserForeignKeys')
 BEGIN
   DROP PROCEDURE
       proc_RemoveAllCmsUserForeignKeys;
 END;

-- exec proc_RemoveAllCmsUserForeignKeys ;
GO
CREATE PROCEDURE proc_RemoveAllCmsUserForeignKeys (
  @PreviewOnly NVARCHAR (10) = 'NO')
AS

/*-----------------------------------------------------------------------------------------------------
Author:   W. Dale Miller
Date:     01.22.2016
Purpose:          Modifies the ROOT table BASE_CMS_User such that it has the surrogate key as the primary key.
*/
BEGIN
 DECLARE
     @MySql NVARCHAR (2000)
    , @FgnKeyNAME NVARCHAR (1000)
    , @GenSql NVARCHAR (1000)
    , @ReferencedTable NVARCHAR (1000)
    , @ParentTableName NVARCHAR (1000)
    , @PKName AS NVARCHAR (250) = '';

 --DECLARE
 --       @PreviewOnly NVARCHAR (10) = 'NO';

 BEGIN TRANSACTION TX001;

 BEGIN TRY
   BEGIN TRY
      ALTER TABLE dbo.BASE_cms_user DISABLE CHANGE_TRACKING;
   END TRY
   BEGIN CATCH
      exec PrintImmediate  'CT Off on BASE_CMS_User';
   END CATCH;

   -- Get all FKeys in the database
   EXEC ScriptAllForeignKeyConstraints ;
   -- Get all FKeys on BASE_CMS_User
   EXEC proc_GetTableForeignKeys 'BASE_CMS_User';

   DECLARE C5 CURSOR
      FOR SELECT
              ForeignKeyNAME
             , GenSql
             , ReferencedTable
             , ParentTableName
         FROM ##FK_Constraints
         WHERE
              ReferencedTable = 'BASE_CMS_User';

   OPEN C5;
   FETCH NEXT FROM C5 INTO @FgnKeyNAME , @GenSql , @ReferencedTable , @ParentTableName;

   -- Remove all constraints
   WHILE
        @@FETCH_STATUS = 0
      BEGIN
         --ALTER TABLE dbo.FACT_TrackerData DROP CONSTRAINT FK_FACT_TrackerData_BASE_HFit_TrackerMealPortions
         SET @MySql = 'ALTER TABLE dbo.' + @ParentTableName + ' DROP CONSTRAINT ' + @FgnKeyNAME;
                exec PrintImmediate @MySql ;
         IF
              @PreviewOnly != 'YES'
            BEGIN
               BEGIN TRY
                  EXEC (@MySql) ;
               END TRY
               BEGIN CATCH
                  PRINT 'ERROR: ' + @MySql;
               END CATCH;
            END;
         FETCH NEXT FROM C5 INTO @FgnKeyNAME , @GenSql , @ReferencedTable , @ParentTableName;
      END;

   CLOSE C5;
   DEALLOCATE C5;
   IF EXISTS (SELECT
                 CONSTRAINT_NAME
           FROM INFORMATION_SCHEMA.TABLE_CONSTRAINTS
           WHERE
                 CONSTRAINT_TYPE = 'PRIMARY KEY' AND
                 TABLE_NAME = 'BASE_CMS_User' AND
                 TABLE_SCHEMA = 'dbo')
      BEGIN
         -- Get the PK name
         SET @PKName = (SELECT
                     CONSTRAINT_NAME
                    FROM INFORMATION_SCHEMA.TABLE_CONSTRAINTS
                    WHERE
                     CONSTRAINT_TYPE = 'PRIMARY KEY' AND
                     TABLE_NAME = 'BASE_CMS_User' AND
                     TABLE_SCHEMA = 'dbo') ;
         -- Drop the PK
         SET @MySql = 'ALTER TABLE [dbo].[BASE_cms_user] DROP CONSTRAINT ' + @PKName;
         exec PrintImmediate  @MySql;
         IF
              @PreviewOnly != 'YES'
            BEGIN
               EXEC (@MySql) ;
            END;
      END;
   IF NOT EXISTS (SELECT
                   CONSTRAINT_NAME
              FROM INFORMATION_SCHEMA.TABLE_CONSTRAINTS
              WHERE
                   CONSTRAINT_TYPE = 'PRIMARY KEY' AND
                   TABLE_NAME = 'BASE_CMS_User' AND
                   TABLE_SCHEMA = 'dbo')
      BEGIN
         ALTER TABLE dbo.BASE_cms_user
         ADD
                  CONSTRAINT PKEY_BASE_cms_user PRIMARY KEY CLUSTERED
                  (
                  SurrogateKey_cms_user ASC
                  )
                  WITH (PAD_INDEX = OFF , STATISTICS_NORECOMPUTE = OFF , SORT_IN_TEMPDB = OFF , IGNORE_DUP_KEY = OFF , ONLINE = OFF , ALLOW_ROW_LOCKS = ON , ALLOW_PAGE_LOCKS = ON) ON [PRIMARY];
      END;

   -- Create a unique performance index on the three common columns
   IF NOT EXISTS (SELECT
                   name
              FROM sys.indexes
              WHERE
                   name = 'PK00_BASE_cms_user')
      BEGIN
         CREATE UNIQUE NONCLUSTERED INDEX PK00_BASE_cms_user ON dbo.BASE_cms_user
         (
         UserID ASC ,
         SVR ASC ,
         DBNAME ASC
         )
         INCLUDE (HashCode , LASTMODIFIEDDATE) WITH (PAD_INDEX = OFF , STATISTICS_NORECOMPUTE = OFF , SORT_IN_TEMPDB = OFF , DROP_EXISTING = OFF , ONLINE = OFF , ALLOW_ROW_LOCKS = ON , ALLOW_PAGE_LOCKS = ON) ON [PRIMARY]
      END;

   ALTER TABLE dbo.BASE_CMS_User ENABLE CHANGE_TRACKING
         WITH (TRACK_COLUMNS_UPDATED = ON) ;
   COMMIT TRANSACTION TX001;
 END TRY
 BEGIN CATCH
   PRINT 'ERROR: ' + @MySql;
   EXECUTE usp_GetErrorInfo;
   ROLLBACK TRANSACTION TX001;
 END CATCH;
 print 'COMPLETED...' ;
END;

GO
PRINT 'Executed proc_RemoveAllCmsUserForeignKeys.sql';
GO

</textarea>
</td></tr>

<tr class="bar">
 <td align="left"><a href="javascript:hidecode(sourcecode);" id="hidelink">Hide code</a></td>
 <td align="right"><a href="http://www.aivosto.com/visustin.html">Visustin flow chart for T-SQL</a></td></tr>
</table>

</body>
</html>
